plugins {
    id 'com.android.library'
    id 'maven-publish'
}

def lib_namespace = 'org.bitmapdecoder'
def lib_version = project.properties['libversion'] ?: '0.1.0-SNAPSHOT'
def lib_github_url = 'https://github.com/Alexander--/bitmapdecoder'

android {
    namespace lib_namespace
    compileSdk 33

    defaultConfig {
        minSdk 21

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        consumerProguardFiles 'consumer-rules.pro'
    }
    externalNativeBuild {
        cmake {
            version '3.22.1'
            path 'src/main/c/CMakeLists.txt'
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }

    ndkVersion '25.1.8937393'
}

dependencies {
    api 'androidx.annotation:annotation:1.0.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

afterEvaluate {
    publishing {
        publications {
            register('release', MavenPublication) {
                groupId lib_namespace
                artifactId 'png'
                version = lib_version

                from(components['release'])

                pom {
                    url.set lib_github_url

                    description.set 'Alternative decoder for images'

                    scm {
                        url.set lib_github_url

                        connection.set 'scm:git:https://github.com/Alexander--/bitmapdecoder.git'
                        developerConnection.set 'scm:git:ssh://git@github.com/Alexander--/bitmapdecoder.git'
                        tag.set 'HEAD'
                    }

                    developers {
                        developer {
                            id.set 'alexanderr'
                            name.set 'Alexander'
                            email.set 'alexander.r@gmx.com'
                        }
                    }

                    licenses {
                        license {
                            name.set 'The Apache License, Version 2.0'
                            url.set 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                }
            }
        }

        repositories {
            maven {
                url = "file://$project.rootDir/repo/maven2"
            }
        }
    }
}